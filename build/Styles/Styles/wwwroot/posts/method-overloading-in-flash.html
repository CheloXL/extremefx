<!DOCTYPE html>
<html dir="ltr" lang="en">
<head>
	<meta charset="utf-8" />
	<meta name="viewport" content="width=device-width,initial-scale=1,maximum-scale=1,user-scalable=0,viewport-fit=cover" />
	<meta http-equiv="imagetoolbar" content="no">

	<title>Method overloading in flash</title>

	<link href="http://fonts.googleapis.com/css?family=Roboto:400,300|Roboto+Condensed:300" rel="stylesheet" type="text/css">
	<link href="/assets/styles/site.css" rel="stylesheet" />
</head>
<body>
	<div class="wrapper">
		<header>
			<hr />
			<div>
				<p><strong><a href="/">Extreme|FX</a></strong> <span>Marcelo Volmaro on Programming, User Experience and anything else.</span></p>

						<nav>
								<a href="/posts" data-icon>Archive</a>
								<a href="/tags" data-icon>Tags</a>
								<a href="/about" data-icon>About Me</a>
								<a href="/contact" data-icon>Contact</a>
						</nav>
			</div>
		</header>

		
<article>
	<div class="article-header">
		<p class="publishing-date">Posted on <time datetime='2009-03-27 00:00:00Z'><span>27</span> <span>mar. 09</span></time></p>
		<h2>Method overloading in flash</h2>
	</div>

	<p>As you may already know, it is not possible to have the same method with different signatures in flash (also known as method overloading).</p>
<p>For example, on c# you may have the same function that does different things depending on the argument count/arguments types. The only limitation is that you can not have a method with the same signature (and different return type, for example).</p>
<p>So, these are perfectly valid method declarations:</p>
<pre><code class="language-csharp">class Datetools {
	public function minus (date:Date):Timespan { ... }
	public function minus (span:Timespan):Date { ... }
}
</code></pre>
<p>And the compiler then chooses the right one based on usage.</p>
<p>Now, how to do this in actionscript? The most used method is to add a switch/if statement inside the function and try to match the parameters. It is not only cumbersome but also hard to maintain.</p>
<p>So, using a little of black magic here and there, I present here my method overloader class.</p>
<pre><code class="language-actionscript">package extremefx.tools {
	/**
	 * &#64;author Marcelo Volmaro
	 */
	 public final class Overloader {
	 	private var _methods:Object;

	 	public function Overloader(pTarget:Object, pFnc:String) {
	 		_methods = {};
	 		pTarget[pFnc] = _exec;
	 	}

	 	public function overload(pFunction:Function, ...args):void {
	 		_methods[args.join(&quot;)] = new Fnc(pFunction, args);
		}

		public function unoverload(...args):Boolean {
			return delete _methods[args.join(&quot;)];
		}

		private function _exec(...args):* {
			var l:int = args.length, h:String = &quot;, theF:Fnc;

			for(var i:int = 0; i &lt; l;i++){
				if (args[i] == null) continue;
				h += args[i].constructor;
			}

			//an exact match was found, use this.
			if ((theF = _methods[h])) return theF.f.apply(null, args);

			var bestMatch:int = -1,
			fnc:Function = null;

			for each(theF in _methods){
				if (theF.l != l) continue;//match number of arguments
				var m:int = 0;
				for (var j:uint = 0; j &lt; l; j++){
					if (args[j] is theF.a[j]) m++;//find out which method has the best match
				}

				if (m &gt; bestMatch){
					bestMatch = m;
					fnc = theF.f;
				}
			}

			return (fnc == null) ? null : fnc.apply(null, args);
		}
	}
}

final class Fnc {
	public var f:Function;
	public var a:Array;
	public var l:uint;
	public function Fnc(pF:Function, args:Array){
		f = pF;
		a = args;
		l = args.length;
	}
}
</code></pre>
<h2 id="how-to-use">How to use?</h2>
<p>Simple. You need to instantiate a new Overloader class, passing as arguments the target object and the name of the function you want to overload. The overloaded function must be defined as a variable of type <em>Function</em>. The Overloader class will assign then the method that will check for arguments and calls the appropiate function.</p>
<pre><code class="language-actionscript">public function main():void {
	var o:Overloader = new Overloader(this, &quot;testFnc&quot;);
	o.overload(_testO1, String);
	o.overload(_testO2, String, Boolean);
	o.overload(_testO3, Number);
	o.overload(_testO4, Function, Number);
	o.overload(_testO5, DummyTest, Number);

	trace(testFnc(&quot;A String&quot;));//A String
	trace(testFnc(&quot;A String&quot;, true));//A String:true
	trace(testFnc(&quot;A String&quot;, false));//A String:false
	trace(testFnc(123.456));//123.456
	trace(testFnc(_testO1, 123.456));//Function plus:123.456
	trace(testFnc(this, 123.456));//Class [object DummyTest] plus:123.456
}

private function _testO1(pString:String):String {
	return pString;
}

private function _testO2(pString:String, pBool:Boolean):String {
	return pString + &quot;:&quot;+ pBool;
}

private function _testO3(pNumber:Number):Number {
	return pNumber;
}

private function _testO4(pFunction:Function, pNumber:Number):String {
	return &quot;Function plus:&quot;+pNumber;
}

private function _testO5(pClass:DummyTest, pNumber:Number):String {
	return &quot;Class &quot;+pClass+&quot; plus:&quot;+pNumber;
}

public var testFnc:Function;
</code></pre>
<p>I'd tried to make it bulletproof. If the specified parameter combination does not exist on the registered function list, the overloader will try to find out the best match from all the available functions, or at least one of the available functions. Please note that since flash player does runtime checking on the argument types, an exception may be thrown if the player can not do
a type convert on the arguments (int, uint and numbers are convertibles, for example).</p>
<p>Hope you like it and as always, comments are welcomed!.</p>


	<p class="tags">Posted under
				<a href="/tags/Flash,-Code,-Example">Flash, Code, Example</a>
	</p>

	<a href="/posts">Back to posts</a>
</article>


	</div>
	<footer>
		<hr />
		<p>© 2005 - 2020 Extreme|FX. All rights reserved. Trademarks are property of their respective owners.</p>
	</footer>
</body>
</html>

